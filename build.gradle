subprojects {
	apply plugin: 'java'
	apply plugin: 'maven-publish'
	apply plugin: 'signing'

	group = 'com.github.team401.coppercore'

	java { 
		withJavadocJar()
		withSourcesJar()
	}

	publishing {
		publications {
			mavenJava(MavenPublication) {
				from components.java
				groupId project.group
				artifactId project.name
				version project.version
				versionMapping {
					usage('java-api') {
						fromResolutionOf('runtimeClasspath')
					}
					usage('java-runtime') {
						fromResolutionResult()
					}
				}
				pom {
					name = project.name
					description = 'Core library for FRC Team 401'
					url = 'http://www.github.com/team401/coppercore'
					licenses {
						license {
							name = 'MIT License'
							url = 'https://opensource.org/license/mit'
						}
					}
					developers {
						developer {
							id = 'aidnem'
							name = 'Aiden Mathai'
						}
						developer {
							id = 'linglejack'
							name = 'Jack Lingle'
						}
						developer {
							id = 'avidraccoon'
							name = 'Nathan Smith'
						}
						developer {
							id = 'minh'
							name = 'Minh Ngyuen'
						}
					}
				}
			}
		}
		repositories {
			maven {
				// change URLs to point to your repos, e.g. http://my.org/repo
				def releasesRepoUrl = layout.buildDirectory.dir('repos/releases')
				def snapshotsRepoUrl = layout.buildDirectory.dir('repos/snapshots')
				url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
			}
		}
	}

	// signing {
	// 	sign publishing.publications.mavenJava
	// }

	javadoc {
		if(JavaVersion.current().isJava9Compatible()) {
			options.addBooleanOption('html5', true)
		}
	}
}
